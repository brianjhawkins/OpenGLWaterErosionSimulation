#version 460 core
layout(local_size_x = 32, local_size_y = 32) in;

layout(rgba32f, binding = 0) uniform image2D image_output;

layout(rgba32f, binding = 1) uniform image2D CD_image;

layout(rgba32f, binding = 2) uniform image2D V_image;

void main()
{    
	ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);

	vec2 imageSize = imageSize(image_output);
    ivec2 deltaX = ivec2(1, 0);
	ivec2 deltaY = ivec2(0, 1);

	vec4 centerColumn = imageLoad(CD_image, pixelCoords);
	vec4 centerVelocity = imageLoad(V_image, pixelCoords);

	// Sediment Capacity constant
	float Kc = 0.002f;
	// Dissolving constant
	float Ks = 0.003f * max(0, (1.0f - 2.0f * centerColumn.a));
	// Deposition constant
	float Kd = 0.001f;

	float newTerrainHeight;
	float newSedimentValue;
	float localTiltAngle;

	// Calculate normal of vector for current point
	vec3 normal;
	vec2 texelSize = vec2(1.0f / imageSize.x, 1.0f / imageSize.y);
	float leftHeight = imageLoad(CD_image, pixelCoords - deltaX).g;
	float rightHeight = imageLoad(CD_image, pixelCoords + deltaX).g;
	float topHeight = imageLoad(CD_image, pixelCoords + deltaY).g;
	float bottomHeight = imageLoad(CD_image, pixelCoords - deltaY).g;
	normal = vec3(leftHeight - rightHeight, 2 * texelSize.x, bottomHeight - topHeight);
	normal = normalize(normal);

	vec3 flatNormal = normalize(vec3(0.0f, 1.0f, 0.0f));

	// Dot product between vertical normal and calculated normal vectors
	// Should be a value between -1 and 1, otherwise acos is undefined for it
	localTiltAngle = acos(dot(flatNormal, normal));

	float sedimentCapacity = Kc * sin(localTiltAngle) * length(centerVelocity);

	// If sediment capacity of water is greater than current amount of sediment dissolved into water
	// dissolve some more terrain into the water and adjust terrain height and sediment value accordingly
	if(sedimentCapacity > centerColumn.b){
		newTerrainHeight = centerColumn.g - Ks * (sedimentCapacity - centerColumn.b);
		newSedimentValue = centerColumn.b + Ks * (sedimentCapacity - centerColumn.b);
	}
	// If sediment capacity of water is not greater than current amount of sediment dissolved into water
	// release some sediment back into the terrain and adjust terrain height and sediment value accordingly
	else{
		newTerrainHeight = centerColumn.g + Kd * (centerColumn.b - sedimentCapacity);
		newSedimentValue = centerColumn.b - Kd * (centerColumn.b - sedimentCapacity);
	}

	imageStore(image_output, pixelCoords, vec4(centerColumn.r, newTerrainHeight, newSedimentValue, centerColumn.a));
}