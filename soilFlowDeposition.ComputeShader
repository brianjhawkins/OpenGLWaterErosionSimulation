#version 460 core
layout(local_size_x = 32, local_size_y = 32) in;

layout(rgba32f, binding = 0) uniform image2D CD_image_output;

layout(rgba32f, binding = 1) uniform image2D CD_image;

layout(rgba32f, binding = 2) uniform image2D S_image;

layout(rgba32f, binding = 3) uniform image2D SC_image;

uniform float width;
uniform float height;
uniform float pipeLength;

void main()
{    
	ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);

    ivec2 deltaX = ivec2(1, 0);
	ivec2 deltaY = ivec2(0, 1);

	vec4 centerColumn = imageLoad(CD_image, pixelCoords);
	vec4 centerSValues = imageLoad(S_image, pixelCoords);
	vec4 centerSCValues = imageLoad(SC_image, pixelCoords);

	float newTerrainValue = centerColumn.a;
	float volumeChange = 0;
	// Subtract deposited values from center column height
	volumeChange -= centerSValues.r;
	volumeChange -= centerSValues.g;
	volumeChange -= centerSValues.b;
	volumeChange -= centerSValues.a;
	volumeChange -= centerSCValues.r;
	volumeChange -= centerSCValues.g;
	volumeChange -= centerSCValues.b;
	volumeChange -= centerSCValues.a;

	// Left inflow soil
	if(pixelCoords.x != 0){
		volumeChange += imageLoad(S_image, pixelCoords - deltaX).g;
	}

	// Right inflow soil
	if(pixelCoords.x != width - 1){
		volumeChange += imageLoad(S_image, pixelCoords + deltaX).r;
	}

	// Top inflow soil
	if(pixelCoords.y != height - 1){
		volumeChange += imageLoad(S_image, pixelCoords + deltaY).a;
	}

	// Bottom inflow soil
	if(pixelCoords.y != 0){
		volumeChange += imageLoad(S_image, pixelCoords - deltaY).b;
	}

	// Bottom left inflow soil
	if(pixelCoords.x != 0 && pixelCoords.y != 0){
		volumeChange += imageLoad(SC_image, pixelCoords - deltaX - deltaY).a;
	}

	// Bottom right inflow soil
	if(pixelCoords.x != width - 1 && pixelCoords.y != 0){
		volumeChange += imageLoad(SC_image, pixelCoords + deltaX - deltaY).b;
	}

	// Top left inflow soil
	if(pixelCoords.x != 0 && pixelCoords.y != height - 1){
		volumeChange += imageLoad(SC_image, pixelCoords - deltaX + deltaY).g;
	}

	// Top right inflow soil
	if(pixelCoords.x != width - 1 && pixelCoords.y != height - 1){
		volumeChange += imageLoad(SC_image, pixelCoords + deltaX + deltaY).r;
	}

	newTerrainValue += (volumeChange / (pipeLength * pipeLength));

	imageStore(CD_image_output, pixelCoords, vec4(centerColumn.r, centerColumn.g, centerColumn.b, newTerrainValue));
}